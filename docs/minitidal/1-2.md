![DECODED Banner](images/banner_minitidal.png)

# Taller d'àudio amb MiniTidal - Seqüenciació i conceptes de patrons en Mini-notació (~1h)

**Objectius:**
 - Descobrir `stack` per construir múltiples clàusules `sound` a la mateixa cel·la.
 - Aplicar la notació `n` per referenciar diferents índexs de mostres dins d'un conjunt.
 - Entendre els ritmes euclidians i la seva notació i ús dins de Tidal.

**Referència:**
 - https://estuary.mcmaster.ca
 - https://tidalcycles.org/docs/

## Configura les cel·les!

```
!presetview twocolumns
```

Tria MiniTidal en el menú desplegable d'idioma a les dues cel·les.

---


## Ús de `stack` per manipular múltiples `sound`s a la mateixa cel·la

Dues cel·les no són gaires per treballar-hi quan estàs introduint capes de sons. Sempre podem afegir més cel·les, o més seqüències paral·leles amb `[` `]`, però això pot esdevenir difícil de manejar ràpidament; i fins i tot pot no ser possible si estàs tocant en un grup gran.

Amb `stack` podem apilar múltiples clàusules `sound` en una sola cel·la:

```
stack [
  sound "bd sd",
  sound "hh*8",
  sound "[~ ho]!2"
]
```

Nota que cal separar línies successives amb una coma `,`.

També és possible prefixar una coma en una nova línia (i en molts casos és preferible quan es comenten i descomenten línies):

```
stack [
  sound "bd sd"
  , sound "hh*8"
  , sound "[~ ho]!2"
]
```

---

## La notació `n` per referenciar índexos de mostres

A `1-1` hem après que es poden triar diferents mostres d'una carpeta de mostres utilitzant `:N`, on `N` és l'*índex* o la posició numerada de la mostra en la carpeta especificada.

```
sound "bd:3 hc sd:1 hc bd bd:3 hc sd ho:4"
```

Si tots els sons provenen de la mateixa carpeta, és més fàcil fer un patró utilitzant la notació `n`; `sprvibe` és una sèrie de notes ascendents mostrajades del sintetitzador "supervibe":

```
n "0 3 [4 5] [8 7]" # sound "sprvibe"
```

Això és equivalent a:
```
sound "sprvibe:0 sprvibe:3 [sprvibe:4 sprvibe:5] [sprvibe:8 sprvibe:7]"
```

...però molt més concís! A més, podem fer aritmètica curiosa a partir de `n` i parèntesis:
```
n ("0 3 [4 5] [8 7]" + "0 2") # sound "sprvibe"
```

Prova a canviar la secció addicional i modificar/avaluar els canvis **en viu**.
```
-- exemple
n ("0 3 [4 5] [8 7]" + "2 4 ~") # sound "sprvibe"
```

Com que estem posant la nostra secció addicional entre `" "`, també podem utilitzar mini-notació. Prova a utilitzar-hi un tipus diferent de parèntesis:
```
-- exemple
n ("0 3 [4 5] [8 7]" + "<2 4 ~>") # sound "sprvibe"
```
---

## Patrons euclidians... genials per la música?

[Els patrons euclidians utilitzen una fórmula específica per dividir un cicle en parts, i després encaixar un nombre d'esdeveniments de mostres entre les parts de la forma més uniforme possible](https://medium.com/code-music-noise/euclidean-rhythms-391d879494df).

Per exemple, podem dividir un cicle en `8` parts, i encaixar `4` esdeveniments uniformement entre les parts:
```
sound "bd(4,8)"
```

Això és, `4` esdeveniments de mostres (en aquest cas `bd`) encaixats en una seqüència de longitud `8`, cosa que resulta en un patró com aquest:
```
|x.x.x.x.|x.x.x.x.|
```
**Badall**, oi?

Els patrons euclidians esdevenen interessants rítmicament quan s'utilitzen nombres que no encaixen regularment a la seqüència. El següent exemple encaixa `5` esdeveniments `bd` dins d'una seqüència de `8` caselles:
```
sound "bd(5,8)"
```
Cosa que resulta en un patró com:
```
|x.xx.xx.|x.xx.xx.|
```

Opcionalment, es pot aplicar un òfset o desplaçament al patró euclidià amb un tercer nombre (o paràmetre) que indica el nombre de passos de desplaçament:
```
sound "bd(5,8,1)"
```

Cosa que resulta en un patró com aquest (compara'l amb el patró sense desplaçament de més amunt):
```
|.xx.xx.x|.xx.xx.x|
```

Per descomptat també podem crear-ne patrons amb mini-notació:
```
sound "bd(<3 5>,<8 8 16>,<0 2>)"
```

---

# Exercicis:

1. Utilitzant la notació `n`, investiga els bancs de sons `glitch` i/o `jazz`.
```
n "<[0 1 2 3] [4 5 6 7]>" # sound "glitch"
```

2. Investiga l'ús de diverses mostres simultàniament amb un patró euclidià, què succeeix?
```
sound "[bd sd](3,8)"
```

3. Prova a apilar (`stack`) múltiples mostres al mateix temps amb patrons euclidians:
```
stack [
  sound "bd(5,8)",
  sound "sd(3,8,2)",
  sound "hh(11,16)"
]
```

4. Usant `stack`, prova a activar i desactivar sons amb la sintaxi de comentaris (`--`).

```
stack [
  sound "bd(5,8)",
  -- sound "sd(3,8,2)",
  sound "hh(11,16)"
]
```
---

## Per punts addicionals

5. Es pot ajustar el to d'una mostra amb `note`, amb el mateix format que la notació per `n`. Prova-ho utilitzant només la mostra `gtr:0`.
```
note ("0 3 [4 5] [8 7]" + "<0!3 2>") # sound "gtr:0"
```

---
