![DECODED Banner](images/banner_minitidal.png)

# Taller d'àudio amb MiniTidal - Primers sons (~1.0h)

**Objectius:**
 - Crear codi per fer (i parar) sons a Estuary.
 - Identificar els bancs de sons/mostres disponibles, i com els pots utilitzar.
 - Crear seqüències de sons utilitzant la *mini-notació* de Tidal.
 - Entendre la notació de claudàtors `[]` i claus angulars `<>`.

**Referència:**
 - https://estuary.mcmaster.ca
 - https://tidalcycles.org/docs/

## Configura les cel·les!

```
!presetview twocolumns
```

Tria MiniTidal en el menú desplegable d'idioma a les dues cel·les.

---

## Fent els teus primers sons

A la cel·la de més a l'esquerra, crea un so de bombo (en anglès, _bass drum_) que soni un cop cada **cycle**:

```
sound "bd"
```

Per **avaluar**, o **executar** el codi prem el botó de **Play** associat a la cel·la. També pots fer-ho amb la combinació de tecles `Shift + Enter`.

Anem a avaluar un so a la cel·la de més a la dreta:

```
sound "~ sd"
```

Cada cel·la es pot pensar com un canal d'àudio estèreo. El segon exemple introdueix una seqüència bàsica dins d'un cicle, amb dos elements:

 - un silenci `~`, i un
 - so de caixa (_snare drum_ en anglès) `sd`

El Tidal pren el patró que se li proporciona entre cometes, i el distribueix uniformement en un **cicle**.

---

## Silenci!

El silenci és important, en música i especialment en Tidal. Pots parar les teves fonts d'àudio de diverses maneres:

- Afegint `# silence` al final del block de codi en una cel·la, i avaluant:

```
sound "bd"
# silence
```

Posant al davant `--` (indica un *comentari*) a qualsevol línia que vulguis desactivar:

```
-- sound "bd"
```

O bé esborrant tot el codi de la cel·la, i avaluant la cel·la en blanc; una bona drecera de teclat per això és:

 - Selecciona tot `Ctrl+A`, aleshores
 - `Retrocés` o `Suprimir`, aleshores
 - Avalua `Shift+Enter`

Acostumo a utilitzar aquest mètode perquè és més ràpid, i és molt fàcil recuperar el teu codi amb la drecera de teclat
  - Desfés `Ctrl+Z`

---

## Explorant els sons disponibles a Estuary

Obre una nova pestanya al navegador, i ves a

  https://estuary.mcmaster.ca

Clica a l'opció `Solo Mode`, i escriu el següent dins de la finestra del xat/terminal:

```
!localview audiomap
```

Això mostra una llista amb tots els noms de mostres disponibles que pots utilitzar amb Tidal.

Nota que cada nom de mostra és realment una carpeta, que (habitualment) conté una colla de fitxers de mostra relacionats.

Pots triar una mostra diferent de dins la carpeta afegint un nombre (comptant a partir de `0`):

```
sound "sd:0 sd:1"
```

Per defecte, si no especifiques quin número de mostra s'ha de tocar, el Tidal triarà el primer (`0`).

**Explora!** Troba dues noves mostres que t'agradin (potser t'agrada el so, o el nom de la mostra, tot està bé!)

---

## Introducció a la seqüenciació amb "mini-notació"

El Tidalcycles permet escriure declaracions curtes i concises per crear patrons de so (o del que sigui). El llenguatge dins de TidalCycles que s'utilitza per seqüenciar els sons (és a dir, per especificar l'**ordre** i **posició en el temps** en què el so s'activa) es diu **Mini-notació**.

Gairebé sempre que vegis `" "` en Tidal, el llenguatge entre les cometes és mini-notació.

Comencem creant un bucle simple de bombo i caixa a la cel·la de l'esquerra:

```
sound "bd sd"
```

Com més mostres afegim, més ràpid sonarà la seqüència: el Tidal distribueix tots els sons dins d'un "cicle", la duració del qual és constant.

```
sound "bd hc sd hc bd bd hc sd ho"
```

Pots utilitzar una colla de signes de puntuació que actuen com **"operadors** dins del **patró** per modificar com es comporta.

Començant amb un patró simple de nou, anem a utilitzar l'operado `*`:

```
sound "bd*2 sd"
```

`*N` repetirà la mostra el nombre de vegades que triïs, dins del temps que ocuparia un únic esdeveniment `bd`, o **pas**; prova a incrementar el `2`.

`!N` és un operador similar, però subtilment diferent: també indica que s'ha de repetir la mostra el nombre especificat de vegades, però distribueix el temps uniformement entre tots els esdeveniments, com s'inserissin passos addicionals.

```
sound "bd!2 sd"
```

Això és equivalent a `"bd bd sd"`. Comprova-ho! Després, prova a incrementar el `2`.

`/N` reduirà la quantitat de vegades que un esdeveniment es produeix a un cop cada `N`è cicle:

```
sound "bd/2 sd"
```

Notes com la mostra `bd` només sona cada dos cicles?

Parlant estrictament, l'operador `/` escamparà la seqüència sobre la qual s'aplica en el nombre de cicles que especifiquis. Més endavant veurem com podem aplicar-lo a una subseqüència.

---

## Subseqüències amb `[ ]`

La mini-notació fa un ús extensiu de diversos tipus de parèntesi, cadascun dels quals té un propòsit específic.

Els claudàtors `[ ]` agrupen un conjunt d'esdeveniments en un pas, creant una **sub-seqüència**. Comencem amb un patró senzill de nou:

```
sound "bd hc sd hc"
```

i encabim alguns esdeveniments addicionals (`~ bd`) dins de l'últim pas:

```
sound "bd hc sd [hc ~ bd]"
```

Pots imbricar els claudàtors:

```
sound "bd hc sd [[hc bd] ~ bd*2]"
```

...i pots crear fàcilment **"compassos compostos"**:

```
sound "[bd hc sd] [hc bd hc bd]"
```

Pots fins i tot aplicar els operadors matemàtics mencionats anteriorment a subseqüències:

```
sound "bd hc sd [hc ~ bd]*2"
```
```
sound "bd hc sd [hc ~ bd]!2"
```
```
-- o equivalentment
sound "bd hc sd [hc ~ bd] !"
```
```
sound "bd hc sd [hc ~ bd]/3"
```

*Nota: l'operador `/` realment està distribuint el patró `[hc ~ bd]` uniformement entre els tres "passos" en els tres "cicles"... què passa si canvies el `3` per un `2`?*

Els claudàtors ens permeten afegir múltiples patrons simultanis per crear **polirítmies** fàcilment!

```
sound "[bd bd, sd sd sd, hc hc hc hc]"
```

---

## Patrons per cicle amb `< >`

Sovint no voldràs que un patró es repeteix *tots* els cicles, i per ajudar-te em això existeixen les útils "claus angulars".

Dins de `< >`, els patrons separats per espais s'activaran consecutivament un per cicle:

```
sound "<bd cp bd [cp ho]>"
```

També es poden imbricar:

```
sound "<<bd*4 sd*12> [bd hc]*4 >"
```
---

# Exercicis:

1. Escurça els següents exemples utilitzant les operadors i parèntesis que hem explicat fins ara al taller:

*Pista: posa el patró original en una cel·la, i construeix la teva variació a l'altra cel·la, de manera que puguis activar-los els dos per confirmar.*

```
sound "[bd bd] hc sd hc bd hs sd [hc hc [hc hc]]"
```
```
sound "bd hc sd hc bd hs sd hc"
```
```
sound "bd hc sd [[hc ~ bd] [hc ~ bd]]"
```

## Per punts addicionals

2. Explora l'ús de l'operador `@`.
```
sound "bd@1 sd@1"
```

3. Explora l'ús de les claus `{` `}`.
```
sound "{bd sd}%4"
```

---
