![DECODED Banner](images/banner_punctual.png)

# Taller de visuals amb Punctual 1-1 - Primeres formes (~1h)

**Objectius:**
 - Crear codi per mostrar diverses formes a Estuary utilitzant la notació de Punctual.
 - Canviar paràmetres per controlar la mida i posició de la forma.

**Referència:**
 - https://estuary.mcmaster.ca
 - https://github.com/dktr0/Punctual/blob/main/REFERENCE.md

## Configura les cel·les!

```
!presetview fulltexteditor
```

Tria **Punctual** en el menú desplegable d'idioma.

---

## Formes i sintaxi

```
circle [0,0] 0.25 >> video;
```
La posició `[0,0]` és el centre de la pantalla. Una coordenada positiva mou el centre cap a la **dreta** (eix `x`) o **amunt** (eix `y`). Un valor negatiu el mou a l'**esquerra** (eix `x`) o **avall** (eix `y`).

Prova a moure'l:
```
circle [0.5,-0.8] 0.25 >> video;
```
*Nota que moure'l més d'una unitat en qualsevol direcció, situarà la coordenada a fora de la pantalla!*

Canvia la seva mida (radi) modificant el tercer nombre:
```
circle [0.5,-0.8] 0.75 >> video;
```

`circle` està definit a la [referència de Punctual](https://github.com/dktr0/Punctual/blob/main/REFERENCE.md) com:
```
circle [x,y,...] [r] -- retorna 1 quan el fragment actual es troba dins del cercle situat a (x,y) amb radi r
```

Anem a aplicar aquesta definició al nostre exemple, de manera que puguem entendre totes les definicions que hi ha a la [referència de Punctual](https://github.com/dktr0/Punctual/blob/main/REFERENCE.md).

Primer de tot, podem veure que `circle` coincideix amb el nom de la **funció**:
```
circle ... >> video;
```

El nostre primer conjunt de parèntesis `[ ]` conte les coordenades `[x,y,...]` - això es coneix com un **argument** o **paràmetre** d'una funció.

```
circle [0.5,-0.8] ... >> video;
```

I l'últim **argument** o **paràmetre** és el radi `[r]` del cercle:

```
circle [0.5,-0.8] 0.25 >> video;
```
Els `...` dins dels claudàtors significa que pots especificar més coordenades `x` i `y` a la funció `circle`. Prova-ho!
```
circle [0.5,-0.8,0.2,0.3] 0.25 >> video;
```
Ara tenim dos cercles; pots fer-ne tants com vulguis!

---

## Més formes

Aquí hi ha més funcions per formes, i les seves definicions a la [referència de Punctual](https://github.com/dktr0/Punctual/blob/main/REFERENCE.md).

`vline` és una línia vertical; les línies en Punctual sobrepassen l'amplada o alçada (en aquest cas) de la pantalla, així que per a una `vline` només necessitem una coordenada `x`, i el gruix de la línia o amplada (`w`).

*Nota que prefixar una línia amb `--` la converteix en un "comentari", que el Puncutal ignorarà*

```
-- vline [x] [w]
vline 0.25 0.02 >> video;
```

De forma similar amb `hline` (línia horitzontal), excepte en l'eix de les `y`.
```
-- hline [y] [w]
hline 0.3 0.02 >> video;
```

`rect` és un rectangle que necessita tant unes coordenades `[x,y]`, com una amplada (`w`) i una alçada (`h`):
```
-- rect [x,y,...] [w,h,...]
rect [0,0] [0.5,0.6] >> video;
```

`point` és només un píxel (i molt petit!). Té una amplada i alçada fixades, per tant només necessita arguments per les coordenades `[x,y]`.
```
-- point [x,y,...]
point [0.5,0.5] >> video;
```
És tan petit! Pots trobar-lo, basant-te en les coordenades donades?

---

## Canals de la sortida de `video`

Podem reemplaçar qualsevol nombre individual amb una **llista** de nombres continguts entre `[ ]`. Quan fem això obtenim diferents resultats en diferents "canals" de color de la sortida de `video`: vermell (`red`), verd (`green`), i blau (`blue`).

Posa múltiples arguments a la coordenada `[x]` per crear tres objectes `vline`, un per cadascun dels canals `red`, `green`, i `blue`:
```
vline [-0.25,0,0.25] 0.01 >> video;
```
Nota que totes les línies tenen el mateix gruix; per què?

Si no especifiques una posició per alments tres canals, els canals restants compartiran l'última valor de la llista.

Per comprovar-ho crea una `vline` especificant només dues coordenades `x`:
```
vline [-0.25,0.25] 0.01 >> video;
```

Fixa't que la línia de la dreta (la que té `0.25` com a coordenada `x`) és d'un color blau clar, el qual és una combinació dels canals `green` i `blue` (que estan compartint la mateixa coordenada `x`).

En el següent exemple, especifiquem **quatre** coordenades `x`. Els primers 3 valors s'assignen a `red`, `green` i `blue` respectivament, però el quart valor crea una quarta `vline` blanca. Per què passa això?

```
vline [0.25, 0, -0.25, 0.5] 0.01 >> video;
```

Els valors també es poden especificar com a fraccions, utilitzant la sintaxi del [llenguatge Haskell](https://www.haskell.org/).

```
vline [(-2)/3, 0, 2/3] 0.01 >> video;
```
---

# Exercicis:

1. Crea una escena amb almenys dues formes diferents.

2. Prova a modificar l'amplada d'una `vline`. Quin valor del paràmetre `w` omple la pantalla?
```
vline 0 0.002 >> video;
```

3. Com d'ample és una línia d'amplada `1`?

## Per punts addicionals

4. Usant `vline`, pots fer una pantalla completament blava?

5. A partir de l'exemple de sota, pots dividir la pantalla en terços (utilitzant qualsevol de les formes), amb cada terç mostrant un color `RGB` diferent?

```
vline [(-2)/3, 0, 2/3] 0.002 >> video;
```

---
